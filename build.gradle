plugins {
	id 'java'
	id "org.jetbrains.kotlin.jvm" version "1.6.21"
	id 'jacoco'
	id 'com.github.spotbugs' version '5.0.8'
	id 'checkstyle'
	id "org.jlleitschuh.gradle.ktlint" version "10.3.0"
}
group = 'net.benelog.actions.experiments'
version = '0.0.2-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.compileJava {
	dependsOn compileKotlin
	classpath += files(compileKotlin.destinationDir)
}

tasks.compileKotlin {
	kotlinOptions {
		jvmTarget = "17"
	}
}

tasks.compileTestKotlin {
	kotlinOptions {
		jvmTarget = "17"
		languageVersion = "1.7"
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
	testImplementation 'org.assertj:assertj-core:3.23.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		csv.enabled true
		csv.destination = file("${buildDir}/reports/jacoco/jacoco.csv")
	}
}


spotbugs {
	toolVersion = '4.7.0'
	ignoreFailures = true
	reportLevel = 'high'
	effort = 'max'
	showProgress = true
}

spotbugsMain {
	reports {
		xml {
			enabled = true
		}
		html {
			enabled = true
		}
	}
}

checkstyle {
	configFile = file("./naver-checkstyle-rules.xml")
	configProperties = [suppressionFile: "naver-checkstyle-suppressions.xml"]
	toolVersion ="10.3"
}

ktlint {
	version = "0.45.2"
	reporters {
		reporter "checkstyle"
		reporter "plain"
	}
	enableExperimentalRules = true
}
